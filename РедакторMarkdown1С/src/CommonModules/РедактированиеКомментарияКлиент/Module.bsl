
Процедура ПриСменеСтраницыПоляКомментария(Форма,Элемент, ТекущаяСтраница) Экспорт
	
	ПутьКДанным=РедактированиеКомментарияКлиентСервер.ПолучитьПутьКДаннымИЗПутиБезЛишнего(РедактированиеКомментарияКлиентСервер.ПолучитьПутьКДаннымПоИменаПанелиСтраниц(Элемент.Имя));
	
	Если ТекущаяСтраница=Форма.Элементы[РедактированиеКомментарияКлиентСервер.ПолучитьИмяСтраницыПросмотраКомментария(ПутьКДанным)] Тогда
		
		ТекстДляПросмотра = Вычислить("Форма."+ПутьКДанным);
		
		ДанныеПрисоединенныхФайлов = РедактированиеКомментарияКлиентСервер.ДанныеПрисовединенныхФайлов(Форма);
		
		РедактированиеКомментарияКлиентСервер.СконвертироватьИменаПрисоединенныхФайловВТексте(ДанныеПрисоединенныхФайлов, ТекстДляПросмотра);
		
		Документ=Форма.Элементы[РедактированиеКомментарияКлиентСервер.ПолучитьИмяЭлементаПросмотраКомментария(ПутьКДанным)].Документ.defaultView;
		Документ.clearTexts();
		Документ.addText("preview", ТекстДляПросмотра);
		Документ.markdownConvert();
		
	Конецесли;
	
КонецПроцедуры

Процедура ОбработкаКомандыПоляКомментария(Форма,Команда) Экспорт
	ТекстЗамены="";
	
	СтруктураСтрокMD=Новый Структура;
	СтруктураСтрокMD.Вставить("ТекстMDНачалоВыделения","");
	СтруктураСтрокMD.Вставить("ТекстMDКонцаВыделения","");
	СтруктураСтрокMD.Вставить("ТекстMDНачалоСтроки","");
	СтруктураСтрокMD.Вставить("ТекстMDСтрокаДо","");
	СтруктураСтрокMD.Вставить("ТекстMDСтрокаПосле","");
	
	Если СтрНайти(Команда.Имя,"КомандаMDЖирный")=1 Тогда
		СтруктураСтрокMD.ТекстMDНачалоВыделения = "**";
		СтруктураСтрокMD.ТекстMDКонцаВыделения = "**";
		
		ТекстЗамены= "КомандаMDЖирный_";
	ИначеЕсли СтрНайти(Команда.Имя,"КомандаMDКурсив")=1 Тогда
		СтруктураСтрокMD.ТекстMDНачалоВыделения = "*";
		СтруктураСтрокMD.ТекстMDКонцаВыделения = "*";
		
		ТекстЗамены= "КомандаMDКурсив_";
	ИначеЕсли СтрНайти(Команда.Имя,"КомандаMDЗачеркнутый")=1 Тогда
		СтруктураСтрокMD.ТекстMDНачалоВыделения = "~~";
		СтруктураСтрокMD.ТекстMDКонцаВыделения = "~~";
		
		ТекстЗамены= "КомандаMDЗачеркнутый_";
	ИначеЕсли СтрНайти(Команда.Имя,"КомандаMDМаркерыСписок")=1 Тогда
		СтруктураСтрокMD.ТекстMDНачалоСтроки="* ";
		
		ТекстЗамены= "КомандаMDМаркерыСписок_";
	ИначеЕсли СтрНайти(Команда.Имя,"КомандаMDНумерованныйСписок")=1 Тогда
		СтруктураСтрокMD.ТекстMDНачалоСтроки="1. ";
		
		ТекстЗамены= "КомандаMDНумерованныйСписок_";
	ИначеЕсли СтрНайти(Команда.Имя,"КомандаMDДобавитьЗаголовки")=1 Тогда
		СтруктураСтрокMD.ТекстMDНачалоСтроки="# ";
		
		
		ТекстЗамены= "КомандаMDДобавитьЗаголовки_";
	ИначеЕсли СтрНайти(Команда.Имя,"КомандаMDЦитата")=1 Тогда
		СтруктураСтрокMD.ТекстMDНачалоСтроки="> ";
		
		ТекстЗамены= "КомандаMDЦитата_";
		
	ИначеЕсли СтрНайти(Команда.Имя,"КомандаMDВставитьИзображениеИзБуфераОбмена")=1 Тогда
		
		ПутьКФайлу = РаботаСКартинкамиКлиент.КартинкаИзБуфераВФайл();
		Если ЗначениеЗаполнено(ПутьКФайлу) Тогда
			
			ИмяФайла = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(ПутьКФайлу).Имя;
			СтруктураСтрокMD.ТекстMDНачалоВыделения = СтрШаблон("![](%1)", ИмяФайла);
			
			Если Форма.ТаблицаПрисоединенныхФайлов.НайтиСтроки(
				Новый Структура("ИмяФайла", ИмяФайла)).Количество() = 0 Тогда
			
				ДобавитьНовыйПрисоединенныйФайл(ИмяФайла, ПутьКФайлу, Форма);
			Иначе
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Файл с таким именем уже добавлен");
			КонецЕсли;
			
		КонецЕсли;
		ТекстЗамены= "КомандаMDВставитьИзображениеИзБуфераОбмена_";
		
	ИначеЕсли СтрНайти(Команда.Имя,"КомандаMDДобавитьФайл")=1 Тогда
		
		ПараметрыВыполнения = Новый Структура("ФормаВладелец", Форма);
		ДиалогВыбора = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
		Обработчик = Новый ОписаниеОповещения("ДобавитьИзФайловойСистемыБезРасширенияПослеЗагрузкиФайла", ЭтотОбъект, ПараметрыВыполнения);
		НачатьПомещениеФайла(Обработчик, , ДиалогВыбора, , Форма.УникальныйИдентификатор);
		
		Возврат;
		
	ИначеЕсли СтрНайти(Команда.Имя,"ВставитьПрисоединенныйФайл")=1 Тогда
		
		ИмяДляПоиска = СтрЗаменить(Команда.Имя, "ВставитьПрисоединенныйФайл", "");
		Поз = Найти(ИмяДляПоиска, "_"); 
		ИдентификаторСтроки = Число(Лев(ИмяДляПоиска, Поз-1));
		СтрокаФайла = Форма.ТаблицаПрисоединенныхФайлов.НайтиПоИдентификатору(ИдентификаторСтроки);
		ИмяФайла = СтрокаФайла.ИмяФайла;
		
		Если ЗначениеЗаполнено(ИмяФайла) Тогда
			СтруктураСтрокMD.ТекстMDНачалоВыделения = СтрШаблон("![](%1)", ИмяФайла);
		КонецЕсли;
		ТекстЗамены= "ВставитьПрисоединенныйФайл"+ИдентификаторСтроки+"_";
		
	Иначе
		Возврат;
	КонецЕсли;
	
	ИмяЭлементаФормы=Сред(Команда.Имя,СтрДлина(ТекстЗамены)+1);
	
	ГраницыВыделения=Новый Структура("НачалоСтроки,КонецСтроки,НачалоКолонки,КонецКолонки");
	Форма.Элементы[ИмяЭлементаФормы].ПолучитьГраницыВыделения(ГраницыВыделения.НачалоСтроки, ГраницыВыделения.НачалоКолонки, ГраницыВыделения.КонецСтроки, ГраницыВыделения.КонецКолонки);
	
	ПутьКДанным=СтрЗаменить(ИмяЭлементаФормы,"_",".");
	
	ТекстКомментария=Вычислить("Форма."+ПутьКДанным);
	
	КомандаMDДобавитьШаблон(ТекстКомментария,ГраницыВыделения,СтруктураСтрокMD);	

	МассивПути=СтрРазделить(ПутьКДанным,".",Ложь);
	Если МассивПути.Количество()=1 Тогда
		Форма[МассивПути[0]]=ТекстКомментария;
	ИначеЕсли МассивПути.Количество()=2 Тогда
		Форма[МассивПути[0]][МассивПути[1]]=ТекстКомментария;
	Иначе
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьИзФайловойСистемыБезРасширенияПослеЗагрузкиФайла(Помещен, Адрес, ВыбранноеИмяФайла, ПараметрыВыполнения) Экспорт
	
	Если Не Помещен Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураПути = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(ВыбранноеИмяФайла);
	ДобавитьНовыйПрисоединенныйФайл(СтруктураПути.Имя, ВыбранноеИмяФайла, ПараметрыВыполнения.ФормаВладелец, Адрес);
	
КонецПроцедуры

Процедура ДобавитьНовыйПрисоединенныйФайл(ИмяФайла, ПутьКФайлу, Форма, Адрес = Неопределено)
	
	Если Адрес = Неопределено Тогда
		Адрес = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ПутьКФайлу), Форма.УникальныйИдентификатор);
	КонецЕсли;
	
	СтрокаПрисоединенногоФайла = Форма.ТаблицаПрисоединенныхФайлов.Добавить();
	СтрокаПрисоединенногоФайла.ПутьКФайлу = ПутьКФайлу;
	СтрокаПрисоединенногоФайла.ИмяФайла = ИмяФайла;
	СтрокаПрисоединенногоФайла.Адрес = Адрес;
	СтрокаПрисоединенногоФайла.ЭтоНовый = Истина;

	Форма.Подключаемый_ПриДобавленииПрисоединенногоФайла(ИмяФайла, СтрокаПрисоединенногоФайла.ПолучитьИдентификатор());
	
КонецПроцедуры

Процедура УдалитьНовыйПрисоединенныйФайл(Форма,ИдентификаторСтроки) Экспорт
	
	Элементы = Форма.Элементы;

	ГруппаПрисоединенногоФайла = Элементы.Найти("Группа_ПрисоединенныйФайл_"+ИдентификаторСтроки);
	Если ГруппаПрисоединенногоФайла <> Неопределено Тогда
		ГруппаПрисоединенногоФайла.Видимость = Ложь;
	КонецЕсли;
	
	СтрокаФайла = Форма.ТаблицаПрисоединенныхФайлов.НайтиПоИдентификатору(ИдентификаторСтроки);
	СтрокаФайла.ПутьКФайлу = "";
	СтрокаФайла.ИмяФайла = "";
	СтрокаФайла.Адрес = "";
	
КонецПроцедуры

Процедура ПриНажатииПоляПросмотраКомментария(Форма,Элемент, ДанныеСобытия, СтандартнаяОбработка) Экспорт
	СтандартнаяОбработка=Ложь;
	
	Если СтрНачинаетсяС(ДанныеСобытия.Element.id,"Файл_") Тогда
		ИдентификаторФайла=СтрЗаменить(ДанныеСобытия.Element.id,"Файл_","");
		СсылкаНаФайл=УправлениеЗадачами.ПолучитьСсылкуНаПрисоединенныйФайлПоИдентификатору(Новый УникальныйИдентификатор(ИдентификаторФайла));
		ОткрытьПрисоединенныйФайлПоСсылке(СсылкаНаФайл,Форма.УникальныйИдентификатор);
	ИначеЕсли ЗначениеЗаполнено(ДанныеСобытия.href) Тогда
		ЗапуститьПриложение(ДанныеСобытия.href);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОткрытьПрисоединенныйФайл(Форма,ИдентификаторСтроки) Экспорт
	
	СтрокаФайла = Форма.ТаблицаПрисоединенныхФайлов.НайтиПоИдентификатору(ИдентификаторСтроки);
	ЗапуститьПриложение(СтрокаФайла.ПутьКФайлу);
	
КонецПроцедуры

Процедура ОткрытьПрисоединенныйФайлПоСсылке(СсылкаНаПрисоединенныйФайл,УникальныйИдентификатор) Экспорт
	ДанныеФайла = РаботаСФайламиСлужебныйВызовСервера.ДанныеФайлаДляОткрытия(СсылкаНаПрисоединенныйФайл, Неопределено, УникальныйИдентификатор);
	Если ДанныеФайла.Зашифрован Тогда
		// Файл может быть изменен в другом сеансе.
		ОповеститьОбИзменении(СсылкаНаПрисоединенныйФайл);
		Возврат;
	КонецЕсли;
	
	РаботаСФайламиКлиент.ОткрытьФайл(ДанныеФайла,Ложь);

КонецПроцедуры

Процедура КомандаMDДобавитьШаблон(ТекстКомментария,ГраницыВыделения,СтруктураСтрокMD)
	
	ТекстовыйДокумент=Новый ТекстовыйДокумент;
	ТекстовыйДокумент.УстановитьТекст(ТекстКомментария);
	
	Если ЗначениеЗаполнено(СтруктураСтрокMD.ТекстMDНачалоВыделения) Тогда
		//В начало вставляем данную строку
		СтрокаВставки=ТекстовыйДокумент.ПолучитьСтроку(ГраницыВыделения.НачалоСтроки);
		
		СтрокаВставки=Лев(СтрокаВставки,ГраницыВыделения.НачалоКолонки-1)
						+СтруктураСтрокMD.ТекстMDНачалоВыделения
						+Сред(СтрокаВставки,ГраницыВыделения.НачалоКолонки);
						
		ТекстовыйДокумент.УдалитьСтроку(ГраницыВыделения.НачалоСтроки);
		ТекстовыйДокумент.ВставитьСтроку(ГраницыВыделения.НачалоСтроки,СтрокаВставки);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтруктураСтрокMD.ТекстMDКонцаВыделения) Тогда
		//В начало вставляем данную строку
		СтрокаВставки=ТекстовыйДокумент.ПолучитьСтроку(ГраницыВыделения.КонецСтроки);
		
		КоличествоСимоловНачалаВыделения=0;
		Если ГраницыВыделения.НачалоСтроки=ГраницыВыделения.КонецСтроки Тогда
			КоличествоСимоловНачалаВыделения=СтрДлина(СтруктураСтрокMD.ТекстMDНачалоВыделения);	
		КонецЕсли;
		
		СтрокаВставки=Лев(СтрокаВставки,ГраницыВыделения.КонецКолонки+КоличествоСимоловНачалаВыделения-1)
						+СтруктураСтрокMD.ТекстMDКонцаВыделения
						+Сред(СтрокаВставки,ГраницыВыделения.КонецКолонки+КоличествоСимоловНачалаВыделения);
						
		ТекстовыйДокумент.УдалитьСтроку(ГраницыВыделения.КонецСтроки);
		ТекстовыйДокумент.ВставитьСтроку(ГраницыВыделения.КонецСтроки,СтрокаВставки);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтруктураСтрокMD.ТекстMDНачалоСтроки) Тогда
		Для НомерСтроки=ГраницыВыделения.НачалоСтроки ПО ГраницыВыделения.КонецСтроки Цикл
			СтрокаВставки=ТекстовыйДокумент.ПолучитьСтроку(НомерСтроки);
			СтрокаВставки=СтруктураСтрокMD.ТекстMDНачалоСтроки+СтрокаВставки;
			
			ТекстовыйДокумент.УдалитьСтроку(НомерСтроки);
			ТекстовыйДокумент.ВставитьСтроку(НомерСтроки,СтрокаВставки);
		КонецЦикла;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтруктураСтрокMD.ТекстMDСтрокаДо) Тогда
		ТекстовыйДокумент.ВставитьСтроку(ГраницыВыделения.НачалоСтроки,СтруктураСтрокMD.ТекстMDСтрокаДо);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтруктураСтрокMD.ТекстMDСтрокаПосле) Тогда
		ТекстовыйДокумент.ВставитьСтроку(ГраницыВыделения.НачалоСтроки+?(ЗначениеЗаполнено(СтруктураСтрокMD.ТекстMDСтрокаДо),1,0),СтруктураСтрокMD.ТекстMDСтрокаПосле);
	КонецЕсли;
	
	
	ТекстКомментария=ТекстовыйДокумент.ПолучитьТекст();
КонецПроцедуры 

Процедура MDДобавитьПереводСтроки(ТекстКомментария)
	Если ЗначениеЗаполнено(ТекстКомментария) Тогда	
		ТекстКомментария = ТекстКомментария 
			+ Символы.ПС + Символы.ПС;
	Конецесли;	
КонецПроцедуры 

Процедура ОбновитьИсториюЗадачи(Задача,ЭлементФормыИстории,Форма) Экспорт 
	МассивИстории=УправлениеЗадачами.ПолучитьМассивИсторииЗадачи(Задача,Форма.УникальныйИдентификатор,РедактированиеКомментарияКлиентСервер.ДанныеПрисовединенныхФайлов(Форма));
	
	ДокументИстории=ЭлементФормыИстории.Документ.defaultView;
	КонтейнерКомментариев=ДокументИстории.document.getElementById("wiki-container");
	КонтейнерКомментариев.innerHTML="";
	
	Для Каждого ТекЗаписьИстории ИЗ МассивИстории Цикл
		ВывестиЭлементИсторииВHTMLдокумент(ДокументИстории,ТекЗаписьИстории,КонтейнерКомментариев,Форма.УникальныйИдентификатор);
	КонецЦикла;

	КонтейнерКомментариев.scrollTop=КонтейнерКомментариев.scrollHeight;

КонецПроцедуры

Функция ФайлИзВложенийЗадачиПоИмени(ИмяФайла,СоответствиеВложений) 
	
КонецФункции

Процедура ВывестиЭлементИсторииВHTMLдокумент(Документ,ЭлементИстории,КонтейнерКомментариев,ИдентификаторФормы)
	Идентификатор=Строка(ЭлементИстории.Ссылка.УникальныйИдентификатор());
	
	КонтейнерЭлементаИстории = Документ.document.createElement("div");
	КонтейнерЭлементаИстории.className = "comment";
	КонтейнерЭлементаИстории.id = Идентификатор;
	
	//<h4>
	Заголовок = Документ.document.createElement("h4");

	//<a href="#note-1" class="journal-link">#1</a>
	СсылкаНаЭлементИстории=Документ.document.createElement("a");
	СсылкаНаЭлементИстории.className="journal-link";
	СсылкаНаЭлементИстории.text= "#" +Идентификатор;
	СсылкаНаЭлементИстории.href="#"+Идентификатор;
	
	Заголовок.appendChild(СсылкаНаЭлементИстории);

	//gravatar
	// <img alt="" title="" class="gravatar" width="24" height="24"
	//srcset = "//www.gravatar.com/avatar/758c6388a3ec9a2e7ca320047727ba57?rating=PG&amp;size=48&amp;default= 2x"
	//src = "//www.gravatar.com/avatar/758c6388a3ec9a2e7ca320047727ba57?rating=PG&amp;size=24x24&amp;default=" >
	Аватарка = Документ.document.createElement("img");
	Аватарка.className = "gravatar";
	Аватарка.width=24;
	Аватарка.height=24;
	АдресКартинки=ПоместитьВоВременноеХранилище(БиблиотекаКартинок.Обсуждения.ПолучитьДвоичныеДанные(),ИдентификаторФормы);
	Аватарка.src=АдресКартинки;//"https://www.gravatar.com/avatar/758c6388a3ec9a2e7ca320047727ba57?rating=PG&amp;size=24x24&amp;default=";
	Аватарка.srcset=АдресКартинки;//"https://www.gravatar.com/avatar/758c6388a3ec9a2e7ca320047727ba57?rating=PG&amp;size=48&amp;default= 2x";
            
	Заголовок.appendChild(Аватарка);
	
	ОБновлено=Документ.document.createDocumentFragment();
	ОБновлено.textContent=ЭлементИстории.Заголовок;
	Заголовок.appendChild(ОБновлено);

	//<a href="/people/4">Сергей Левкин</a>   
	Редактор = Документ.document.createElement("a");
	Редактор.textContent=СокрЛП(ЭлементИстории.Автор);
	//author.className = 'journal-link';
	//commentLink.text = "#" + key;
	Редактор.href = "#" + Идентификатор;
	
	Заголовок.appendChild(Редактор);
	
	//<a title="22.08.2019 09:21" href="/projects/aviavto/activity?from=2019-08-22">около 3 часа</a>
	ДатаИзменения = Документ.document.createElement("a");
	ДатаИзменения.textContent = " "+Формат(ЭлементИстории.ДатаСоздания,"ДФ='dd.MM.yyyy ЧЧ:мм'");
	ДатаИзменения.title=Формат(ЭлементИстории.ДатаСоздания,"ДФ='dd.MM.yyyy ЧЧ:мм'");
	
	//author.className = 'journal-link';
	//commentLink.text = "#" + key;
	ДатаИзменения.href = "#" + Идентификатор;
	Заголовок.appendChild(ДатаИзменения);

	КонтейнерЭлементаИстории.appendChild(Заголовок);
	
	//Если у нас есть вложения к комментарию выводим это ссылками
	Если ЭлементИстории.МассивВложений.Количество()>0 Тогда
		СписокЭлемент=Документ.document.createElement("ul");
		СписокЭлемент.className="details";
		
		Для Каждого ТекВложение ИЗ ЭлементИстории.МассивВложений Цикл
			ВложениеЭлемент=Документ.document.createElement("li");
			
			ЗаголовокВложения=Документ.document.createElement("strong");
			ЗаголовокВложения.textContent="Файл";
			ВложениеЭлемент.appendChild(ЗаголовокВложения);
			
			СсылкаНаФайл = Документ.document.createElement("a");
			СсылкаНаФайл.href = ПолучитьНавигационнуюСсылку(ТекВложение.Ссылка);
			СсылкаНаФайл.id="Файл_"+ТекВложение.Ссылка.УникальныйИдентификатор();
			СсылкаНаФайл.title = ТекВложение.ИмяФайла;
			СсылкаНаФайл.textContent=ТекВложение.ИмяФайла;
			ВложениеЭлемент.appendChild(СсылкаНаФайл);
			
			СписокЭлемент.appendChild(ВложениеЭлемент);
		КонецЦикла;
		
		КонтейнерЭлементаИстории.appendChild(СписокЭлемент);
	КонецЕсли;
	
	//<div id="journal-46030-notes" class="wiki editable">
	Комментарий = Документ.document.createElement("div");
	Комментарий.className="wiki editable";
	Комментарий.innerHTML = Документ.converter.makeHtml(ЭлементИстории.ТекстСообщения);
	КонтейнерЭлементаИстории.appendChild(Комментарий);
	
	КонтейнерКомментариев.appendChild(КонтейнерЭлементаИстории);
	
	
	
КонецПроцедуры

